#include <Arduino.h>
#include "images.h"
#include <esp_sleep.h>
/*–Ω–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è –≤–µ–± —ñ–Ω—Ç–µ—Ä—Ñ–µ–π—Å—É*/
#include <GyverDBFile.h>
#include <LittleFS.h>
GyverDBFile db(&LittleFS, "/data.db");
#include <SettingsGyver.h>
SettingsGyver sett("SONIAH", &db);
/*–Ω–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è –∫–ª—é—á—ñ–≤ –ë–î*/
enum kk : size_t {
  wifiSsid,
  wifiPass,
  brightnessValue,
  switchPosition1,
  switchPosition2,
  displayMode,
  themeColor,
  language,
  apply
};
/*–≥–ª–æ–±–∞–ª—å–Ω—ñ –∑–º—ñ–Ω–Ω—ñ –±–µ–∑ –ø–æ—Ç—Ä–µ–±–∏ —É –ø–æ—Å—Ç—ñ–π–Ω–æ–º—É –∑–±–µ—Ä—ñ–≥–∞–Ω–Ω—è –≤ –µ–Ω–µ—Ä–≥–æ–Ω–µ–∑–∞–ª–µ–∂–Ω—ñ–π –ø–∞–º'—è—Ç—ñ*/
struct Data {
  int batteryChargePercent = 0;
  bool wifiConnecting = false;
  uint32_t wifiConnectStart = 0;
};
Data data;
/*—Å—Ç—Ä—É–∫—Ç—É—Ä–∞ –∫–æ–Ω—Ñ—ñ–≥—É—Ä–∞—Ü—ñ—ó —Ä–æ–∑–º—ñ—Ä—É –æ—á–µ–π —Ä–æ–±–æ—Ç–∞*/
struct RoboEyesConfig {
  const uint8_t EYE_WIDTH = 24;
  const uint8_t EYE_HEIGHT = 24;
  const uint8_t BORDER_RADIUS = 8;
  const uint8_t SPACE_BETWEEN = 4;
};

  RoboEyesConfig roboeyesconfig;
/*—Å—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è —É–ø—Ä–∞–≤–ª—ñ–Ω–Ω—è –∂–∏–≤–ª–µ–Ω–Ω—è–º*/
struct PowerManagement {
  const float ADC_VOLTAGE_MULTIPLIER = 3.3 * 1.8 / 4095.0;
  const float BATTERY_MAX_VOLTAGE = 4.1; // –º–∞–∫—Å–∏–º–∞–ª—å–Ω–∞ –Ω–∞–ø—Ä—É–≥–∞ –±–∞—Ç–∞—Ä–µ—ó
  const float BATTERY_MIN_VOLTAGE = 3.2; // –º—ñ–Ω—ñ–º–∞–ª—å–Ω–∞ –Ω–∞–ø—Ä—É–≥–∞ –±–∞—Ç–∞—Ä
  const int DIODE_DROP_MAH = 50; // –ø–∞–¥—ñ–Ω–Ω—è –Ω–∞–ø—Ä—É–≥–∏ –Ω–∞ –¥—ñ–æ–¥–∞—Ö –≤ –º–ê
  const int BATTERY_CAPACITY_MAH = 2800; // —î–º–Ω—ñ—Å—Ç—å –±–∞—Ç–∞—Ä–µ—ó –≤ –º–ê–≥
  const int ESP32_CONSUMPTION_MAH = 80; // —Å–µ—Ä–µ–¥–Ω—î —Å–ø–æ–∂–∏–≤–∞–Ω–Ω—è ESP32 –≤ –º–ê
  const int MAX_LIGHT_CONSUMPTION_MAH = 200; // –º–∞–∫—Å–∏–º–∞–ª—å–Ω–µ —Å–ø–æ–∂–∏–≤–∞–Ω–Ω—è –ª—ñ—Ö—Ç–∞—Ä—è –≤ –º–ê
  const int BRIGHTNESS_MULTIPLIER = 255;
};
  PowerManagement powerManagement;

/*—Å—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è –ª–æ–∫–∞–ª—ñ–∑–∞—Ü—ñ—ó - —Ü—è —á–∞—Å—Ç–∏–Ω–∞ —â–µ –≤ —Ä–æ–∑—Ä–æ–±—Ü—ñ*/ 
struct Lang {
  // –í–∫–∞–∑—É—î–º–æ —Ä–æ–∑–º—ñ—Ä [2], –æ—Å–∫—ñ–ª—å–∫–∏ —É –Ω–∞—Å 2 –º–æ–≤–∏
  const char* BATTERY[2] = {"Battery Charge", "<–ó–∞—Ä—è–¥ –±–∞—Ç–∞—Ä–µ—ó>"};
  const char* MAINSETTINGS[2] = {"Main Settings", "–û—Å–Ω–æ–≤–Ω—ñ –Ω–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è"};
  const char* WIFI[2] = {"WiFi", "WiFi"};
  const char* SSID[2] = {"ssid", "–Ω–∞–∑–≤–∞ –º–µ—Ä–µ–∂—ñ"};
  const char* PASSWORD[2] = {"password", "–ø–∞—Ä–æ–ª—å"};
  const char* THEMECOLOR[2] = {"Theme Color", "–ö–æ–ª—ñ—Ä –¢–µ–º–∏"};
  const char* LANGUAGE[2] = {"Language", "–ú–æ–≤–∞"};
  const char* SAVEBUTTON[2] = {"Save & Restart", "–ó–±–µ—Ä–µ–≥—Ç–∏ —Ç–∞ –ü–µ—Ä–µ–∑–∞–≤–∞–Ω—Ç–∞–∂–∏—Ç–∏"};
  const char* LIGHTSETTINGS[2] = {"Flashlight settings", "–ù–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è —Å–≤—ñ—Ç–ª–∞"};
  const char* BRIGHTNESS[2] = {"Brightness slider", "–Ø—Å–∫—Ä–∞–≤—ñ—Å—Ç—å"};
  const char* POSITION1[2] = {"Position 1", "–ü–æ–∑–∏—Ü—ñ—è –ø–µ—Ä–µ–º–∏–∫–∞—á–∞ 1"};
  const char* POSITION2[2] = {"Position 2", "–ü–æ–∑–∏—Ü—ñ—è –ø–µ—Ä–µ–º–∏–∫–∞—á–∞ 2"};
  const char* DISPLAYMODE[2] = {"Display mode", "–ù–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è –µ–∫—Ä–∞–Ω—É"};
  


};

Lang lng;

const int PWM_CHANNEL = 0;
const int PWM_FREQ = 2000;        // 2 kHz - –º–∞–∫—Å–∏–º—É–º –¥–ª—è LDO6AJSA
const int PWM_RESOLUTION = 8; 

/*–±—ñ–±–ª—ñ–æ—Ç–µ–∫–∏ –¥–ª—è —Ä–æ–±–æ—Ç–∏ –æ–ª–µ–¥ –¥–∏—Å–ø–ª–µ—è*/
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <FluxGarage_RoboEyes72x40.h>

#define SdaPin 5
#define SclPin 6

#define ScreenWidth 128
#define ScreenHeight 64
#define OledReset    -1
Adafruit_SSD1306 display(ScreenWidth, ScreenHeight, &Wire, OledReset); 

RoboEyes<Adafruit_SSD1306> roboEyes(display);

/*=============–±—ñ–Ω–¥ –ø—ñ–Ω—ñ–≤=================*/
#define voltmeterPin 3 // –ø—ñ–Ω –¥–ª—è –≤–æ–ª—å—Ç–º–µ—Ç—Ä–∞
//–ø—ñ–Ω–∏ –¥–ª—è –≤–∏–∑–Ω–∞—á–µ–Ω–Ω—è –ø–æ–∑–∏—Ü—ñ—ó –ø–µ—Ä–µ–º–∏–∫–∞—á–∞ –ª—ñ—Ö—Ç–∞—Ä–∏–∫–∞
#define positionOnepin 1 //–¥–æ–¥–∞—Ç–∏ –Ω–æ–º–µ—Ä –ø—ñ–Ω–∞ 
#define positionTwopin 2 //–¥–æ–¥–∞—Ç–∏ –Ω–æ–º–µ—Ä –ø—ñ–Ω–∞ 
//–ø—ñ–Ω–∏ –¥–ª—è –∫–µ—Ä—É–≤–∞–Ω–Ω—è –∫–æ–ª—å–æ—Ä–æ–º —Å–≤—ñ—Ç–ª–∞
#define blueLightPin 4 //–¥–æ–¥–∞—Ç–∏ –Ω–æ–º–µ—Ä –ø—ñ–Ω–∞
#define whiteLightPin 7 //–¥–æ–¥–∞—Ç–∏ –Ω–æ–º–µ—Ä –ø—ñ–Ω–∞
//–ø—ñ–Ω –¥–ª—è –∫–µ—Ä—É–≤–∞–Ω–Ω—è —è—Å–∫—Ä–∞–≤—ñ—Å—Ç—é —Å–≤—ñ—Ç–ª–∞
#define brightnessPin 0 //–¥–æ–¥–∞—Ç–∏ –Ω–æ–º–µ—Ä –ø—ñ–Ω–∞
/*=========================–§–£–ù–ö–¶–Ü–á===========================*/
/*–ø–µ—Ä–µ–≤—ñ—Ä–∫–∞ —ñ –≤—Ö—ñ–¥ —É —Ä–µ–∂–∏–º –≥–ª–∏–±–æ–∫–æ–≥–æ —Å–Ω—É –∑ debounce*/
/*–ø–µ—Ä–µ–≤—ñ—Ä–∫–∞ —ñ –≤—Ö—ñ–¥ —É —Ä–µ–∂–∏–º –≥–ª–∏–±–æ–∫–æ–≥–æ —Å–Ω—É*/
void checkAndEnterDeepSleep() {
    static uint32_t offTimer = 0;
    const unsigned long DEBOUNCE_TIME = 5000;  // 5000 –º—Å –∑–∞—Ç—Ä–∏–º–∫–∞
    
    // –Ø–∫—â–æ –æ–±–∏–¥–≤–∞ –ø—ñ–Ω–∏ LOW - –ø–µ—Ä–µ–º–∏–∫–∞—á –≤ OFF
    if (digitalRead(positionOnepin) == LOW && digitalRead(positionTwopin) == LOW) {
        if (offTimer == 0) {
            offTimer = millis();
        }
        
        // –Ø–∫—â–æ OFF —Å—Ç–∞–Ω —Ç—Ä–∏–º–∞—î—Ç—å—Å—è 500 –º—Å - –π–¥–µ–º–æ —Å–ø–∞—Ç–∏
        if (millis() - offTimer >= DEBOUNCE_TIME) {
            Serial.println("=== ENTERING DEEP SLEEP ===");
            Serial.print("positionOnepin: ");
            Serial.println(digitalRead(positionOnepin));
            Serial.print("positionTwopin: ");
            Serial.println(digitalRead(positionTwopin));
            
            // –í–∏–º–∏–∫–∞—î–º–æ —Å–≤—ñ—Ç–ª–æ–¥—ñ–æ–¥–∏ —ñ PWM
            digitalWrite(blueLightPin, LOW);
            digitalWrite(whiteLightPin, LOW);
            ledcWrite(PWM_CHANNEL, 0);
            
            // –í–∏–º–∏–∫–∞—î–º–æ WiFi
            WiFi.disconnect(true);
            WiFi.mode(WIFI_OFF);
            delay(100);
            
            // –ü–æ–≤—ñ–¥–æ–º–ª–µ–Ω–Ω—è –Ω–∞ –¥–∏—Å–ø–ª–µ–π
            display.clearDisplay();
            display.setCursor(30, 20);
            display.setTextSize(2);
            display.println("Sleep");
            display.display();
            delay(1000);
            
            display.clearDisplay();
            display.display();
            
            // –ù–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è wake-up –ø–æ HIGH —Ä—ñ–≤–Ω—é –Ω–∞ –±—É–¥—å-—è–∫–æ–º—É –∑ –ø—ñ–Ω—ñ–≤
            esp_deep_sleep_enable_gpio_wakeup(1ULL << positionOnepin | 1ULL << positionTwopin, ESP_GPIO_WAKEUP_GPIO_HIGH);
            
            Serial.println("Wake-up configured for GPIO1 or GPIO2");
            Serial.println("Going to sleep NOW...");
            Serial.flush();
            delay(100);
            
            // DEEP SLEEP
            esp_deep_sleep_start();
        }
    } else {
        // –°–∫–∏–¥–∞—î–º–æ —Ç–∞–π–º–µ—Ä —è–∫—â–æ —Ö–æ—á –æ–¥–∏–Ω –ø—ñ–Ω HIGH
        offTimer = 0;
    }
}
/*–≤–∏–∑–Ω–∞—á–µ–Ω–Ω—è –∑–∞—Ä—è–¥—É –±–∞—Ç–∞—Ä–µ—ó —É –≤—ñ–¥—Å–æ—Ç–∫–∞—Ö*/
int batCharge(uint8_t pin) {
    static float emaValue = 0;
    static bool initialized = false;
    
    // –£—Å–µ—Ä–µ–¥–Ω–µ–Ω–Ω—è 5 –≤–∏–º—ñ—Ä—ñ–≤
    const int NUM_SAMPLES = 5;
    int sum = 0;
    for (int i = 0; i < NUM_SAMPLES; i++) {
        sum += analogRead(pin);
        delayMicroseconds(100);  // –º—ñ–∫—Ä–æ—Å–µ–∫—É–Ω–¥–∏ –¥–ª—è —à–≤–∏–¥–∫–æ—Å—Ç—ñ
    }
    int avgRawValue = sum / NUM_SAMPLES;
    
    // –Ü–Ω—ñ—Ü—ñ–∞–ª—ñ–∑–∞—Ü—ñ—è EMA
    if (!initialized) {
        emaValue = avgRawValue;
        initialized = true;
    }
    
    // –ï–∫—Å–ø–æ–Ω–µ–Ω—Ü—ñ–π–Ω–µ –∑–≥–ª–∞–¥–∂—É–≤–∞–Ω–Ω—è
    const float ALPHA = 0.15;  // 15% –Ω–æ–≤–µ, 85% —Å—Ç–∞—Ä–µ
    emaValue = ALPHA * avgRawValue + (1.0 - ALPHA) * emaValue;
    
    // –†–æ–∑—Ä–∞—Ö—É–Ω–æ–∫ –Ω–∞–ø—Ä—É–≥–∏ —ñ –≤—ñ–¥—Å–æ—Ç–∫—ñ–≤
    float voltage = emaValue * powerManagement.ADC_VOLTAGE_MULTIPLIER;
    float percentFloat = ((voltage - powerManagement.BATTERY_MIN_VOLTAGE) / 
                          (powerManagement.BATTERY_MAX_VOLTAGE - powerManagement.BATTERY_MIN_VOLTAGE)) * 100.0;
    
    return constrain((int)percentFloat, 0, 100);
}

/*—Ä–æ–∑—Ä–∞—Ö—É–Ω–æ–∫ –∫—ñ–ª—å–∫–æ—Å—Ç—ñ —á–∞—Å—É, —â–æ –∑–∞–ª–∏—à–∏–≤—Å—è –¥–æ —Ä–æ–∑—Ä—è–¥–∫–∏ –±–∞—Ç–∞—Ä–µ—ó*/
int estimationTimeHours(int chargePercent,int brightnessLevel) {
  int estimatedHours = 0;
  int totalConsumptionMah = brightnessLevel * powerManagement.MAX_LIGHT_CONSUMPTION_MAH / 100 + powerManagement.ESP32_CONSUMPTION_MAH + powerManagement.DIODE_DROP_MAH; // —Ä–æ–∑—Ä–∞—Ö—É–Ω–æ–∫ —Å–ø–æ–∂–∏–≤–∞–Ω–Ω—è –ª—ñ—Ö—Ç–∞—Ä—è –≤ –º–ê –≤ –∑–∞–ª–µ–∂–Ω–æ—Å—Ç—ñ –≤—ñ–¥ —è—Å–∫—Ä–∞–≤–æ—Å—Ç—ñ
  estimatedHours = (chargePercent * powerManagement.BATTERY_CAPACITY_MAH / 100 / totalConsumptionMah);  //–ø—Ä–∏–±–ª–∏–∑–Ω–∏–π —Ä–æ–∑—Ä–∞—Ö—É–Ω–æ–∫ —á–∞—Å—É –¥–æ —Ä–æ–∑—Ä—è–¥–∫–∏ –±–∞—Ç–∞—Ä–µ—ó –≤ –≥–æ–¥–∏–Ω–∞—Ö
  return estimatedHours;
}
/*–∫–µ—Ä—É–≤–∞–Ω–Ω—è –≤–∏–±–æ—Ä–æ–º –∫–æ–ª—å–æ—Ä—É —Å–≤—ñ—Ç–ª–∞*/
void bindPositionLight(int position) {
  if (position == 0) {
      digitalWrite(blueLightPin, HIGH);
      digitalWrite(whiteLightPin, LOW);
  }
  if (position == 1) {
      digitalWrite(blueLightPin, LOW);
      digitalWrite(whiteLightPin, HIGH);
  }
}
/*—Ä–µ–≥—É–ª—é–≤–∞–Ω–Ω—è —è—Å–∫—Ä–∞–≤–æ—Å—Ç—ñ —Å–≤—ñ—Ç–ª–∞*/
void adjustBrightness(int brightnessValue) {
  int pwmValue = brightnessValue * powerManagement.BRIGHTNESS_MULTIPLIER / 100;
  ledcWrite(PWM_CHANNEL, pwmValue);  // ‚Üê –∑–∞–º—ñ—Å—Ç—å analogWrite()
}
/*–≤—ñ–¥–æ–±—Ä–∞–∂–µ–Ω–Ω—è –∫–æ–ª—å–æ—Ä—É –≤—ñ–¥–∂–µ—Ç–∞ –±–∞—Ç–∞—Ä–µ—ó*/
sets::Colors batteryWidgetColorChange(int value) {
  if (value < 30) {
      return sets::Colors::Red;
  } else if (value < 70) {
      return sets::Colors::Yellow;
  } else {
      return sets::Colors::Green;
  }
}
/*–≤—ñ–¥–æ–±—Ä–∞–∂–µ–Ω–Ω—è –≤—ñ–¥—Å–æ—Ç–∫—ñ–≤ –∑–∞—Ä—è–¥—É –±–∞—Ç–∞—Ä–µ—ó –Ω–∞ OLED –¥–∏—Å–ø–ª–µ—ó*/
void displayChargeLevel(int info) {
  static uint32_t tmrCharge = 0;
  if (millis() - tmrCharge < 1000) return;  // –æ–Ω–æ–≤–ª—é—î–º–æ –Ω–µ —á–∞—Å—Ç—ñ—à–µ 1 —Ä–∞–∑—É/—Å–µ–∫
  tmrCharge = millis();

  display.clearDisplay();
  if (info == 100) {
      display.setCursor(30,10);
  } else if (info >= 10) {
      display.setCursor(45,10);
  } else {
      display.setCursor(54,10);
  }
  display.println(info);
  display.display();
}
/*–≤—ñ–¥–æ–±—Ä–∞–∂–µ–Ω–Ω—è –∫—ñ–ª—å–∫–æ—Å—Ç—ñ —á–∞—Å—É, —â–æ –∑–∞–ª–∏—à–∏–≤—Å—è –¥–æ —Ä–æ–∑—Ä—è–¥–∫–∏ –±–∞—Ç–∞—Ä–µ—ó –Ω–∞ OLED –¥–∏—Å–ø–ª–µ—ó*/
void displayEstimationTime(int estimatedHours) {
  static uint32_t tmrEstimation = 0;
  if (millis() - tmrEstimation < 1000) return;  // –æ–Ω–æ–≤–ª—é—î–º–æ –Ω–µ —á–∞—Å—Ç—ñ—à–µ 1 —Ä–∞–∑—É/—Å–µ–∫
  tmrEstimation = millis();
    
  display.clearDisplay();
  if (estimatedHours >= 10) {
      display.setCursor(30,10);
  } else {display.setCursor(45,10);
  }
  //display.setCursor(30,10);//–ø–µ—Ä—à–∏–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç - –ø–æ –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª—ñ, –¥—Ä—É–≥–∏–π - –ø–æ –≤–µ—Ä—Ç–∏–∫–∞–ª—ñ 
  display.println(String(estimatedHours) + "H");
  display.display();
}
/*–≤–∏–±—ñ—Ä —Ä–∞–Ω–¥–æ–º–Ω–æ–≥–æ —á–∏—Å–ª–∞*/
int getRandomNumber(int maxValue) {
  return random(1, maxValue + 1);
}
/*–∞–Ω—ñ–º–∞—Ü—ñ—è –æ—á–µ–π —Ä–æ–±–æ—Ç–∞*/
void displayRoboEyesAnimation() {
  roboEyes.update();
  static uint32_t tmrAnimation;
  const unsigned long EYE_ANIMATION_INTERVAL = 3 * 60 * 1000; // —ñ–Ω—Ç–µ—Ä–≤–∞–ª –∑–º—ñ–Ω–∏ –∞–Ω—ñ–º–∞—Ü—ñ—ó –≤ –º—ñ–ª—ñ—Å–µ–∫—É–Ω–¥–∞—Ö (3 —Ö–≤–∏–ª–∏–Ω–∏)
  if (millis() - tmrAnimation >= EYE_ANIMATION_INTERVAL) { // –∫–æ–∂–Ω—ñ 3 —Ö–≤–∏–ª–∏–Ω–∏ –∑–º—ñ–Ω—é—î–º–æ –∞–Ω—ñ–º–∞—Ü—ñ—é
      int randNumFace = getRandomNumber(4);
      tmrAnimation = millis();
      switch (randNumFace) {
          case 1:
              roboEyes.setMood(DEFAULT);
              roboEyes.anim_laugh();
              break;
          case 2:
              roboEyes.setMood(TIRED);
              roboEyes.anim_confused();
              break;
          case 3:
              roboEyes.setMood(ANGRY);
              roboEyes.anim_confused();
              break;
          case 4:
              roboEyes.setMood(HAPPY);
              roboEyes.anim_laugh();
              break;
      }
  }
}
/*–º–∞–ø—ñ–Ω–≥ —Ä–µ–∂–∏–º—ñ–≤ –ø–µ—Ä–µ–º–∏–∫–∞—á–∞ —Ç–∞ –∫–æ–ª—å–æ—Ä—ñ–≤ –ª—ñ—Ö—Ç–∞—Ä—è*/
void manageSwitcherPosition() {
  if (digitalRead(positionTwopin) == LOW) {
      bindPositionLight(db[kk::switchPosition1]);
  }
  if (digitalRead(positionTwopin) == HIGH) {
      bindPositionLight(db[kk::switchPosition2]);
  } 
}
/*----------------------------------------------------------------------------*/
/*=======================================*/



/*—Å—Ç–≤–æ—Ä–µ–Ω–Ω—è –±–ª–æ–∫—ñ–≤ –≤–µ–±  —ñ–Ω—Ç–µ—Ä—Ñ–µ–π—Å—É*/
void build(sets::Builder& b) {
  int lang = (int)db[kk::language];
  b.Image(H(img), "", "/logo.avif");
  b.LinearGauge(H(batCharge), lng.BATTERY[lang], 0, 100, "", data.batteryChargePercent,batteryWidgetColorChange(data.batteryChargePercent));
  if (b.beginGroup("Main Settings")) {
    b.Input(kk::wifiSsid, "SSID");
    b.Pass(kk::wifiPass, "Password");
    b.Select(kk::themeColor, "Theme Color", "Green;Red;Blue;Yellow;Mint;Orange;Pink;Aqua;Violet");  // ‚Üê –¥–æ–¥–∞—Ç–∏
    if (b.Button(kk::apply, "Save & Restart")) {
        db.update();
        ESP.restart();
        
    }
    b.endGroup();
  }
  if (b.beginGroup("Flashlight Settings")) {
      b.Slider(kk::brightnessValue, "Brightness Slider", 0, 100,1);
      if (b.beginRow()) {
        b.LED(H(led1), "Position 1",1, sets::Colors::Yellow,sets::Colors::Blue);
        b.Switch(kk::switchPosition1, "");
        b.LED(H(led2), "",0, sets::Colors::Yellow,sets::Colors::Blue);
        b.endRow();
      }

      if (b.beginRow()) {
        b.LED(H(led3), "Position 2",1, sets::Colors::Yellow,sets::Colors::Blue);
        b.Switch(kk::switchPosition2, "");
        b.LED(H(led4), "",0, sets::Colors::Yellow,sets::Colors::Blue);
        b.endRow();
      }

      b.Select(kk::displayMode, "Display Mode", "Battery Charge;Time to discharge;Robot Eyes");
      b.endGroup();
  }
}

void update(sets::Updater u) {
  u.update(H(batCharge), data.batteryChargePercent);
  u.updateColor(H(batCharge), batteryWidgetColorChange(data.batteryChargePercent));
}

void setup() {
  Serial.begin(115200); // 115200 baud rate
  pinMode(voltmeterPin, INPUT);
  pinMode(positionOnepin, INPUT_PULLDOWN);
  pinMode(positionTwopin, INPUT_PULLDOWN);
  pinMode(blueLightPin, OUTPUT);
  pinMode(whiteLightPin, OUTPUT);

  ledcSetup(PWM_CHANNEL, PWM_FREQ, PWM_RESOLUTION);
  ledcAttachPin(brightnessPin, PWM_CHANNEL);
  //–≤–∏–º–∫–Ω—É—Ç–∏ —Ñ–∞–π–ª–æ–≤–∏–π –º–µ–Ω–µ–¥–∂–µ—Ä 
  sett.config.useFS = false;

  data.batteryChargePercent = batCharge(voltmeterPin);// –ø–µ—Ä—à–∏–π —Ä–∞–∑ –æ—Ç—Ä–∏–º—É—ç–º–æ –∑–Ω–∞—á–µ–Ω–Ω—è –∑–∞—Ä—è–¥—É –±–∞—Ç–∞—Ä–µ—ó. –ù–∞—Å—Ç—É–ø–Ω–∏–π —Ä–∞–∑ –±—É–¥–µ —á–µ—Ä–µ–∑ 5 —Ö–≤–∏–ª–∏–Ω
  // ======== WIFI ========
  // STA
  WiFi.mode(WIFI_AP_STA);
  // ======== SETTINGS ========
  sett.begin(true,"soniah"); // –±–∞–∑—É –¥–∞–Ω–∏—Ö –ø—ñ–¥–∫–ª—é—á–∞—î–º–æ –¥–æ –ø—ñ–¥–∫–ª—é—á–µ–Ω–Ω—è –¥–æ —Ç–æ—á–∫–∏
  sett.setVersion("1.0.0");
  sett.setProjectInfo("SONIAH üåª - smart flashlight");
  sett.onBuild(build);
  sett.onUpdate(update);
  // ======== DATABASE ========
  #ifdef ESP32
    LittleFS.begin(true);
  #else
    LittleFS.begin();
  #endif

  db.begin();

  File existing = LittleFS.open("/logo.avif", "r");
  bool needWrite = !existing || existing.size() != logo_avif_len;
  if (existing) existing.close();

  if (needWrite) {
    File f = LittleFS.open("/logo.avif", "w");
    if (f) {
        f.write(logo_avif, logo_avif_len);
        f.close();
        Serial.println("Logo updated");
    } else {
        Serial.println("Logo write failed");
    }
  } else {
    Serial.println("Logo OK, skip write");
  }

  // —ñ–Ω—ñ—Ü—ñ—é–≤–∞–Ω–Ω—è –ë–î –ø–æ—á–∞—Ç–∫–æ–≤–∏–º–∏ –¥–∞–Ω–∏–º–∏
    db.init(kk::wifiSsid, "");
    db.init(kk::wifiPass, "");
    db.init(kk::brightnessValue, 100);
    db.init(kk::switchPosition1, 0);
    db.init(kk::switchPosition2, 1);
    db.init(kk::displayMode, 2);
    db.init(kk::themeColor, 0);
    db.init(kk::language, 0); // 0 = English –∑–∞ –∑–∞–º–æ–≤—á—É–≤–∞–Ω–Ω—è–º


  //–Ω–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è —Ç–µ–º–∏
  const sets::Colors themes[] = {
    sets::Colors::Green, sets::Colors::Red, sets::Colors::Blue,
    sets::Colors::Yellow, sets::Colors::Mint, sets::Colors::Orange,
    sets::Colors::Pink, sets::Colors::Aqua, sets::Colors::Violet
  };
  sett.config.theme = themes[(int)db[kk::themeColor]];
  // ======= AP =======
  WiFi.softAP("soniahsf","soniahsf");
  Serial.print("AP IP: ");
  Serial.println(WiFi.softAPIP());

  // ======= STA =======
  // –æ–¥–∏–Ω —Ä–∞–∑ –ø—Ä–æ–±—É—î–º–æ –ø—ñ–¥–∫–ª—é—á–∏—Ç–∏—Å—å –¥–æ WiFi —è–∫—â–æ —î –Ω–∞–ª–∞—à—Ç—É–≤–∞–Ω–Ω—è
if (db[kk::wifiSsid].length()) {
    Serial.print("WiFi ‚Üí ");
    Serial.println(db[kk::wifiSsid]);
    WiFi.begin(db[kk::wifiSsid], db[kk::wifiPass]);
    data.wifiConnecting = true;
    data.wifiConnectStart = millis();
}



  /*=======================SETUP OLED========================================*/
  Wire.begin(SdaPin, SclPin);
  if(!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println(F("SSD1306 allocation failed"));
    for(;;);
  }

  display.setRotation(2);
  display.setTextSize(3);
  display.setTextColor(SSD1306_WHITE);
  /*=======================SETUP robo eyes========================================*/
  roboEyes.begin(ScreenWidth, ScreenHeight, 100);
  // Define some automated eyes behaviour
  roboEyes.setAutoblinker(ON, 3, 2); // Start auto blinker animation cycle -> bool active, int interval, int variation -> turn on/off, set interval between each blink in full seconds, set range for random interval variation in full seconds
  // Define eye shapes, all values in pixels
  

  roboEyes.setWidth(roboeyesconfig.EYE_WIDTH, roboeyesconfig.EYE_WIDTH);
  roboEyes.setHeight(roboeyesconfig.EYE_HEIGHT, roboeyesconfig.EYE_HEIGHT);
  roboEyes.setBorderradius(roboeyesconfig.BORDER_RADIUS, roboeyesconfig.BORDER_RADIUS);
  roboEyes.setSpacebetween(roboeyesconfig.SPACE_BETWEEN);
  //roboEyes.setHFlicker(ON, 2); // horizontal flickering effect -> bool active, int intensity (1-5)
  roboEyes.setPosition(NE); // cardinal directions, can be N, NE, E, SE, S, SW, W, NW, DEFAULT (default = horizontally and vertically centered)
  //–≤—ñ—Ç–∞–ª—å–Ω–∞ —Ñ—Ä–∞–∑–∞ –Ω–∞ –æ–ª–µ–¥ –¥–∏—Å–ø–ª–µ—ó
  
  display.clearDisplay();
  display.setTextSize(3);
  display.setTextColor(SSD1306_WHITE);
  display.setCursor(45,10);//–ø–µ—Ä—à–∏–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç - –ø–æ –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª—ñ, –¥—Ä—É–≥–∏–π - –ø–æ –≤–µ—Ä—Ç–∏–∫–∞–ª—ñ 
  display.println("Hi");
  display.display();
  
 // –ø–µ—Ä–µ–∑–∞–≤–∞–Ω—Ç–∞–∂–∏—Ç–∏ —Å—Ç–æ—Ä—ñ–Ω–∫—É –±—Ä–∞—É–∑–µ—Ä–∞
  sett.reload(true);
} 


void loop() {
    if (data.wifiConnecting) {
        if (WiFi.status() == WL_CONNECTED) {
            Serial.println("WiFi: " + WiFi.localIP().toString());
            data.wifiConnecting = false;
        } else if (millis() - data.wifiConnectStart > 10000) {
            Serial.println("WiFi timeout");
            data.wifiConnecting = false;
        }
    }
  checkAndEnterDeepSleep();
  /*======================battery charge manager===================*/
  static uint32_t tmrBattery;
  const unsigned long BATTERY_CHARGE_INTERVAL = 5 * 60 * 1000; // —ñ–Ω—Ç–µ—Ä–≤–∞–ª –æ–Ω–æ–≤–ª–µ–Ω–Ω—è –∑–∞—Ä—è–¥—É –±–∞—Ç–∞—Ä–µ—ó –≤ –º—ñ–ª—ñ—Å–µ–∫—É–Ω–¥–∞—Ö (5 —Ö–≤–∏–ª–∏–Ω)
  if (millis() - tmrBattery >= BATTERY_CHARGE_INTERVAL) { // –∫–æ–∂–Ω—ñ 5 —Ö–≤–∏–ª–∏–Ω –∑–º—ñ–Ω—é—î–º–æ –∞–Ω—ñ–º–∞—Ü—ñ—é
  data.batteryChargePercent = batCharge(voltmeterPin);
  //data.batteryChargePercent = random(0,101); //—Ç–µ—Å—Ç–æ–≤–µ –∑–Ω–∞—á–µ–Ω–Ω—è –∑–∞—Ä—è–¥—É –±–∞—Ç–∞—Ä–µ—ó
  tmrBattery = millis();
  }
  /*======================switcher position manager===================*/
  manageSwitcherPosition();
  /*========================BRIGHTNESS=================================*/
  int lastBrightnessValue = 0;
  if (db[kk::brightnessValue] != lastBrightnessValue) {
    lastBrightnessValue = db[kk::brightnessValue];
    adjustBrightness(db[kk::brightnessValue]);
  }
  /*========================robot eyes=================================*/
  switch ((int)db[kk::displayMode]){
    /*======================== display percent of battery charge=================================*/
    case 0:
      displayChargeLevel(data.batteryChargePercent);
      break;
    /*========================display quantity hours to battery discharge=================================*/
    case 1:
      displayEstimationTime(estimationTimeHours(data.batteryChargePercent,db[kk::brightnessValue]));
      break;
    case 2:
      displayRoboEyesAnimation();
      break;
  }
  sett.tick();
}


